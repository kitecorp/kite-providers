plugins {
    id 'java'
    id 'application'
}

group = 'cloud.kitelang'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':kite-provider-sdk')
    annotationProcessor project(':kite-provider-sdk')

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    implementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.24.1'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'cloud.kitelang.providers.files.FilesProvider'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

test {
    useJUnitPlatform()
    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
}

tasks.named('startScripts') {
    applicationName = 'provider'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'cloud.kitelang.providers.files.FilesProvider'
        )
    }
}

tasks.register('generateProviderManifest') {
    def manifestFile = file("$buildDir/install/files/provider.json")
    outputs.file manifestFile
    doLast {
        manifestFile.parentFile.mkdirs()
        manifestFile.text = """{
    "name": "files",
    "version": "${project.version}",
    "protocolVersion": 1,
    "executable": "bin/provider"
}
"""
    }
}

tasks.named('installDist') {
    finalizedBy 'generateProviderManifest'
}
