plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'cloud.kitelang'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':kite-provider-sdk')
    annotationProcessor project(':kite-provider-sdk')

    // AWS SDK v2
    implementation platform('software.amazon.awssdk:bom:2.28.22')
    implementation 'software.amazon.awssdk:ec2:2.40.7'
    implementation 'software.amazon.awssdk:sts:2.40.7'
    implementation 'software.amazon.awssdk:elasticloadbalancingv2:2.40.7'
    implementation 'software.amazon.awssdk:route53:2.40.7'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // Logging
    implementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.24.1'

    // Testing
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
}

application {
    mainClass = 'cloud.kitelang.provider.aws.AwsProvider'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

test {
    useJUnitPlatform()
    // Required for msgpack DirectByteBuffer access in Java 17+
    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
}

// Create distribution for easy testing
tasks.named('startScripts') {
    applicationName = 'provider'
}

// Shadow JAR configuration - creates a fat JAR with all dependencies
shadowJar {
    archiveBaseName.set('aws-provider')
    archiveClassifier.set('')
    archiveVersion.set('')

    mergeServiceFiles()

    // Minimize the JAR by removing unused classes
    minimize {
        // Don't minimize these - they use reflection
        exclude(dependency('org.apache.logging.log4j:.*'))
        exclude(dependency('io.grpc:.*'))
        exclude(dependency('com.google.protobuf:.*'))
        exclude(dependency('org.msgpack:.*'))
        exclude(project(':kite-provider-sdk'))
        exclude(project(':kite-proto'))
        exclude(project(':kite-api'))
    }

    manifest {
        attributes(
            'Main-Class': 'cloud.kitelang.provider.aws.AwsProvider'
        )
    }
}

// Create a single executable JAR
jar {
    manifest {
        attributes(
            'Main-Class': 'cloud.kitelang.provider.aws.AwsProvider'
        )
    }
}

// Generate provider.json manifest for gRPC provider discovery
tasks.register('generateProviderManifest') {
    def manifestFile = file("$buildDir/install/aws/provider.json")
    outputs.file manifestFile
    doLast {
        manifestFile.parentFile.mkdirs()
        manifestFile.text = """{
    "name": "aws",
    "version": "${project.version}",
    "protocolVersion": 1,
    "executable": "bin/provider"
}
"""
    }
}

// Ensure provider.json is generated with installDist
tasks.named('installDist') {
    finalizedBy 'generateProviderManifest'
}

// Create minimized distribution
tasks.register('installMinDist', Copy) {
    dependsOn shadowJar
    from(shadowJar.archiveFile) {
        into 'lib'
    }
    from('src/main/scripts') {
        into 'bin'
        fileMode = 0755
    }
    into "$buildDir/install/aws-min"

    doLast {
        // Create simple launcher script
        def binDir = file("$buildDir/install/aws-min/bin")
        binDir.mkdirs()
        file("$binDir/provider").text = '''#!/bin/sh
SCRIPT_DIR="$(cd "$(dirname "$0")" && pwd)"
exec java --add-opens=java.base/java.nio=ALL-UNNAMED --add-opens=java.base/sun.nio.ch=ALL-UNNAMED -jar "$SCRIPT_DIR/../lib/aws-provider.jar" "$@"
'''
        file("$binDir/provider").setExecutable(true)

        // Generate provider.json
        file("$buildDir/install/aws-min/provider.json").text = """{
    "name": "aws",
    "version": "${project.version}",
    "protocolVersion": 1,
    "executable": "bin/provider"
}
"""
    }
}
