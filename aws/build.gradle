plugins {
    id 'cloud.kitelang.provider' version '0.2.2'
}

group = 'cloud.kitelang'
version = '0.1.3'

repositories {
    mavenLocal()
    mavenCentral()
}

kiteProvider {
    name = 'aws'
    logoUrl = 'https://upload.wikimedia.org/wikipedia/commons/9/93/Amazon_Web_Services_Logo.svg'
    // mainClass auto-detected from ProviderServer subclass
}

dependencies {
    // AWS SDK v2
    implementation platform('software.amazon.awssdk:bom:2.28.22')
    implementation 'software.amazon.awssdk:ec2:2.40.7'
    implementation 'software.amazon.awssdk:sts:2.40.7'
    implementation 'software.amazon.awssdk:elasticloadbalancingv2:2.40.7'
    implementation 'software.amazon.awssdk:route53:2.40.7'
    implementation 'software.amazon.awssdk:s3:2.40.7'
    implementation 'software.amazon.awssdk:iam:2.40.7'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SLF4J with JDK logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-jdk14:2.0.17'

    // Testing
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

test {
    useJUnitPlatform()
    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
}

// Optional: customize shadowJar minimize if needed
shadowJar {
    minimize {
        exclude(dependency('org.slf4j:.*'))
        exclude(dependency('io.grpc:.*'))
        exclude(dependency('com.google.protobuf:.*'))
        exclude(dependency('org.msgpack:.*'))
        exclude(dependency('cloud.kitelang:.*'))
    }
}
